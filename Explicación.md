
## ‚ú® Explicaci√≥n

Aqu√≠ se explicar√° el c√≥digo **l√≠nea por l√≠nea**:

### üì• Importaci√≥n de `Servo.h`

Se importa la librer√≠a `Servo.h` para el funcionamiento del servomotor. Tambi√©n **se define el nombre que se utilizar√° para emplear la funci√≥n en el c√≥digo**:

```cpp
#include <Servo.h>  // Librer√≠a est√°ndar Servo

Servo servo;  // Servo est√°ndar
```

### ‚öôÔ∏è Definici√≥n de pines

Se **almacenan** en constantes los **n√∫meros de pin a los que se conecta** cada sensor. El del m√≥dulo Bluetooth no se define porque utiliza los pines especiales 0 (RX) y 1 (TX), que no lo requieren.

```cpp
const int sensorPin1 = 12;
const int sensorPin2 = 2;
const int sensorPin3 = 3;
const int buzzerPin = A5;
const int servoPin = 10;
const int ledPin = 8;
const int aguaPin = 7;
```

### üì¶ Definici√≥n de variables

Se definen **tres variables binarias principales**. Por defecto, valen 0 (apagadas).

```cpp
int variable1 = 0;
int variable2 = 0;
int variable3 = 0;
```

### üèÅ Definici√≥n de *flags*

Se crean las *flags*, variables booleanas (verdadero o falso) que **ayudan a mantener el control del estado de ciertas acciones**.

```cpp
bool variable1Activable = false;
bool variable2Activable = false;
bool parpadeoCajaRealizado = false;
bool parpadeoCierreRealizado = false;
bool temporizadorActivo1 = false;
bool temporizadorActivo2 = false;
bool alarmaAguaActivada = false;
```

### üßÆ Otras variables

Se definen las variables, los temporizadores y el monitoreo, adem√°s de algunas *flags* adicionales.

```cpp
unsigned long tiempoActivacion1 = 0;
unsigned long tiempoActivacion2 = 0;
const unsigned long TIEMPO_ESPERA = 2UL * 30UL * 1000UL;

unsigned long ultimoPrint = 0;
const unsigned long intervaloPrint = 1000;

bool alarmaActivada = false;
bool cajaAbierta = false;
```

### üîì `abrirCaja()`

Despu√©s se crea la** primera funci√≥n**, `abrirCaja()`. Lo que hace es activar la flag y girar el motor.

```cpp
void abrirCaja() {
  cajaAbierta = true;
  servo.write(180);  // Abrir al m√°ximo
}
```

### üîê `cerrarCaja()`

Aqu√≠ **se crea la funci√≥n** `cerrarCaja()`, que reinicia las variables y las flags y gira el motor.

```cpp
void cerrarCaja() {
  variable1 = 0;
  variable2 = 0;
  cajaAbierta = false;
  temporizadorActivo1 = false;
  temporizadorActivo2 = false;
  parpadeoCajaRealizado = false;
  parpadeoCierreRealizado = false;
  servo.write(90);  // Posici√≥n cerrada (media posici√≥n)
}
```

### üõ†Ô∏è `setup()`

Aqu√≠ est√° la **funci√≥n setup**, una funci√≥n que por defecto se activa al iniciarse o reiniciarse la placa. Aqu√≠ se definen los pines de los sensores como `INPUT`, y el altavoz y la luz como `OUTPUT`, ya que reciben ordenes. Tambi√©n **se pone el motor en su posici√≥n por defecto**, pero esto se hace **por comodidad al testear**. Adem√°s se inicia la comunicaci√≥n Serial a `38400 baudios` para el bluetooth.

```cpp
void setup() {
  pinMode(sensorPin1, INPUT);
  pinMode(sensorPin2, INPUT);
  pinMode(sensorPin3, INPUT);
  pinMode(buzzerPin, OUTPUT);
  pinMode(ledPin, OUTPUT);
  pinMode(aguaPin, INPUT_PULLUP);

  servo.attach(servoPin);
  servo.write(90);  // Posici√≥n cerrada inicial

  Serial.begin(38400);
}
```

### üì¢ Funciones de alarma (`ejecutarSirena`, `ejecutarSirenaAgua`)

Estas son las **funciones de ejecutar sirena**, son un bucle que con el tiempo y unas variables hacen el tono necesario.

```cpp
void ejecutarSirena() {
  static int freq = 1000;
  static bool subiendo = true;
  static unsigned long ultimaActualizacion = 0;
  const int incremento = 30;
  const unsigned long intervalo = 4;

  if (millis() - ultimaActualizacion >= intervalo) {
    ultimaActualizacion = millis();
    tone(buzzerPin, freq);
    if (subiendo) {
      freq += incremento;
      if (freq >= 4000) subiendo = false;
    } else {
      freq -= incremento;
      if (freq <= 1000) subiendo = true;
    }
  }
}

void ejecutarSirenaAgua() {
  static unsigned long ultimaNota = 0;
  static bool estado = false;
  const unsigned long duracionNota = 300;

  if (millis() - ultimaNota >= duracionNota) {
    ultimaNota = millis();
    estado = !estado;

    if (estado) {
      tone(buzzerPin, 1000);
    } else {
      noTone(buzzerPin);
    }
  }
}
```

### üîÅ `loop()`

Ahora empieza la funci√≥n m√°s importante, el `loop()`. Est√° **es una funci√≥n predeterminada que se ejecuta en bucle** esperando condiciones y actualizaciones. Al inicio, **recoge el estado de los 3 sensores** principales (agua, luz, y magnetismo). El `estadoSensor2` est√° para pruebas con otros sensores si es necesario pero no se utiliza.

```cpp
int estadoSensor1 = digitalRead(sensorPin1);
int estadoSensor2 = digitalRead(sensorPin2);
int estadoSensor3 = digitalRead(sensorPin3);
int estadoSensor4 = digitalRead(aguaPin);
```

### üí° Parpadeo del LED

Despu√©s, se realiza **el encendido y apagado del LED al cerrarse o abrirse la caja** (3 parpadeos), **y al sonar la alarma** (hasta que se apague).

```cpp
  if (cajaAbierta && !parpadeoCajaRealizado) {
    for (int i = 0; i < 3; i++) {
      digitalWrite(ledPin, HIGH); delay(100);
      digitalWrite(ledPin, LOW); delay(100);
    }
    parpadeoCajaRealizado = true;
  } else if (!cajaAbierta && !parpadeoCierreRealizado) {
    for (int i = 0; i < 3; i++) {
      digitalWrite(ledPin, HIGH); delay(100);
      digitalWrite(ledPin, LOW); delay(100);
    }
    parpadeoCierreRealizado = true;
  }

  if (alarmaActivada || alarmaAguaActivada) {
    static unsigned long ultimoCambio = 0;
    static bool estadoLED = false;
    if (millis() - ultimoCambio >= 100) {
      ultimoCambio = millis();
      estadoLED = !estadoLED;
      digitalWrite(ledPin, estadoLED ? HIGH : LOW);
    }
  } else {
    digitalWrite(ledPin, LOW);
  }
```

### üö® Activaci√≥n de alarmas

Aqu√≠ se realiza la **activaci√≥n de la alarma**. En el `if (alarmaActivada)` se da **preferencia a la alarma de robo por encima de la de agua**. Adem√°s, arriba se activan las flags si el sensor detecta algo.

```cpp
 if (!alarmaActivada && estadoSensor3 == HIGH && variable3 == 0) {
    alarmaActivada = true;
  }

  bool alarmaAguaAnterior = alarmaAguaActivada;
  alarmaAguaActivada = (estadoSensor4 == LOW);

  if (alarmaActivada) {
    ejecutarSirena();
  } else if (alarmaAguaActivada) {
    ejecutarSirenaAgua();
  } else {
    noTone(buzzerPin);
  }

  if (alarmaAguaAnterior && !alarmaAguaActivada) {
    servo.detach();
    delay(10);
    servo.attach(servoPin);
    if (cajaAbierta) {
      servo.write(180);
    } else {
      servo.write(90);
    }
  }
```

### üì∂ Activaci√≥n `variable1` (sensor l√≠nea)

Aqu√≠ **se define la activaci√≥n de la variable1**, el sensor de l√≠nea. El primer condicional hace que **solo el sensor detecte algo si las alarmas no est√°n activada**, despu√©s mira el estado del sensor y si est√° encendido hace la variable1 activable. **Despu√©s revisa si el sensor ha detectado algo**, y s**i la variable no estaba ya activada la activa e inicia el contador de 2 minutos** (si la variable 3 no est√° activada), que si se acaba la `variable1` vuelve a 0.

```cpp
  if (!alarmaActivada && !alarmaAguaActivada) {
    if (estadoSensor1 == HIGH) {
      variable1Activable = true;
    }
    if (estadoSensor1 == LOW && variable1 == 0 && variable1Activable) {
      variable1 = 1;
      variable1Activable = false;
      if (variable2 == 1) variable3 = 1;
      if (variable3 == 0) {
        tiempoActivacion1 = millis();
        temporizadorActivo1 = true;
      } else {
        temporizadorActivo1 = false;
      }
    }
  } else {
    variable1Activable = false;
  }
```

### üì≤ Comunicaci√≥n Bluetooth y control `variable2`

Aqu√≠ **se define la activaci√≥n de la variable2**. En el **primer condicional revisa si el sensor est√° activo y espera un mensaje**, que si existe hace la `variable2` activable y lo reenv√≠a para comprobar que ha llegado bien. **Despu√©s, si la `variable2` es activable y el mensaje existe, Elije el mensaje que responder**:
* Si la alarma est√° activada y recibe el c√≥digo, la desactiva y responde "Alarma desactivada"
* Si la alarma est√° activada y el c√≥digo que recibe es incorrecto, responde "Alarma activada, c√≥digo incorrecto."
* Si la alarma de agua est√° activada, responde "Alarma de agua activada, mire si la caja est√° mojada o ll√©vela a reparaci√≥n."
* Si la `variable2` (el bluetooth) y est√° activada y la caja est√° cerrada, responde "Ya se hab√≠a introducido el c√≥digo, introduzca la llave."
* Si la variable2 est√° activada, la caja abierta y el c√≥digo es distinto del c√≥digo de cierre de la caja, responde "Ya se hab√≠a introducido el c√≥digo, la caja est√° abierta."
* Si las `variables 1, 2 y 3` est√°n desactivadas y le llega el c√≥digo correcto, responde "C√≥digo correcto, introduzca la llave.", cambia `variable2 = 1` y activa el `temporizador2`.
* Si la caja est√° abierta y recibe la contrase√±a de cierre, responde "Cerrando caja...", ejecuta `cerrarCaja()` y pone `variable3` a 0
* Si el mensaje es el c√≥digo, estando la caja cerrada, la `variable2` en 0 y la `variable1` en 1, responde "Caja abierta", cambia `variable 2 y 3` a 1 y desactiva el `temporizador2`.
* Si el mensaje es incorrecto, responde "C√≥digo incorrecto"
* Si no se cumple ninguna condici√≥n, responde "Error"
Al final vuelve a reiniciar el mensaje a "".

```cpp
  if (Serial.available()) {
    mensajeBT = Serial.readStringUntil('\n');
    mensajeBT.trim();
    if (mensajeBT != "") {
      variable2Activable = true;
      Serial.println("Codigo recibido: " + mensajeBT);
      delay(10);
    }
  }

  if (variable2Activable && mensajeBT != "") {
    if (alarmaActivada) {
      if (mensajeBT == passwordBT) {
        alarmaActivada = false;
        Serial.println("Alarma desactivada.");
        delay(10);
        variable2 = 0; variable3 = 0;
        temporizadorActivo1 = false; temporizadorActivo2 = false;
      } else {
        Serial.println("Alarma activada, codigo incorrecto.");
        delay(10);
      }
    } else if (alarmaAguaActivada) {
      Serial.println("Alarma de agua activada, mire si la caja est√° mojada o ll√©vela a reparaci√≥n.");
      delay(10);
    } else {
      if (variable2 == 1 && variable3 == 0 && !cajaAbierta) {
        Serial.println("Ya se habia introducido el codigo, introduzca la llave.");
        delay(10);
      } else if (variable2 == 1 && variable3 == 1 && cajaAbierta && mensajeBT != passwordCerrarBT) {
        Serial.println("Ya se habia introducido el codigo, la caja esta abierta.");
        delay(10);
      } else if (mensajeBT == passwordBT && variable3 == 0 && variable2 == 0 && !cajaAbierta && variable1 == 0) {
        Serial.println("Codigo correcto, introduzca la llave.");
        delay(10);
        variable2 = 1;
        tiempoActivacion2 = millis();
        temporizadorActivo2 = true;
      } else if (mensajeBT == passwordCerrarBT && variable3 == 1 && cajaAbierta) {
        variable3 = 0;
        Serial.println("Cerrando caja...");
        delay(10);
      } else if (mensajeBT == passwordBT && variable3 == 0 && variable2 == 0 && !cajaAbierta && variable1 == 1) {
        Serial.println("Caja abierta.");
        delay(10);
        variable2 = 1; variable3 = 1;
        temporizadorActivo2 = false;
      } else if (mensajeBT != passwordBT) {
        Serial.println("Codigo incorrecto.");
        delay(10);
      } else {
        Serial.println("Error.");
        delay(10);
      }
    }
    mensajeBT = "";
  }
```

### ‚è±Ô∏è L√≥gica de temporizadores

Despu√©s empieza **la l√≥gica de contadores**. Ser√≠a m√°s sencillo hacerla con `delays` pero **utiliza `millis()` con un bucle para no parar la funci√≥n loop**. Si pasan 2 minutos y no se ha activado la otra variable, la reinicia.

```cpp
  if (temporizadorActivo1 && millis() - tiempoActivacion1 > TIEMPO_ESPERA) {
    variable1 = 0;
    temporizadorActivo1 = false;
  }

  if (temporizadorActivo2) {
    if (variable1 == 1) {
      temporizadorActivo2 = false;
    } else if (millis() - tiempoActivacion2 > TIEMPO_ESPERA) {
      variable2 = 0;
      temporizadorActivo2 = false;
    }
  }
```

### üì§ Apertura y cierre de caja

Esta parte **abre la caja y apaga los contadores si `variable3` pasa a 1**.

```cpp
  if (variable3 == 1 && !cajaAbierta) {
    abrirCaja();
    temporizadorActivo1 = false;
    temporizadorActivo2 = false;
  } else if (variable3 == 0 && cajaAbierta) {
    cerrarCaja();
  }
```

### üñ•Ô∏è Monitor en tiempo real

Esta parte es del monitor en tiempo real para el desarrollo.

```cpp
if (millis() - ultimoPrint >= intervaloPrint) {
  // printStatus();
  ultimoPrint = millis();
}
```

